#!/usr/bin/perl

# switchbot - Munin multigraph plugin to monitor switchbot sensor
#
# Copyright (C) 2020 Ken-ichi Mito

=head1 NAME

switchbot - Munin multigraph plugin to monitor switchbot sensor

=head1 CONFIGURATION

This plugin does not need any special privilege to fetch data,
but needs to work in combination with tools/switchbot script.

    [switchbot]
        env.listpath /etc/switchbot/devices.txt
        env.datadir  /run/switchbot

=head1 MAGIC MARKERS

 #%# family=manual
 #%# capabilities=multigraph

=head1 AUTHOR

Ken-ichi Mito

=head1 LICENSE

2-clause BSD License

=cut

use strict;
use warnings;
use Carp;
use utf8;

use Munin::Plugin;

my $DEBUG = defined $ENV{'debug'} ? $ENV{'debug'} : 0;
my $plugin_name = $Munin::Plugin::me;

# check current environment supports multigraph plugins
need_multigraph();

# hardcoded constants used to print config messages
my $CONFIG_MESSAGE = {
    Humidity => {
        title_of_value => 'Humidity',
        graph_args     => '--base 1000 --lower-limit 0 --upper-limit 100',
        graph_vlabel   => '%',
        graph_info     => 'This graph shows the percentage of humidity of SwtichBot sensor. The sensor accuracy is +/-2% (25 deg, 10% - 90%), +/-4% (25 deg, 0% - 10% or 90% - 99%).',
    },
    Temperature => {
        title_of_value => 'Temperature',
        graph_args     => '--base 1000',
        graph_vlabel   => 'degrees Celsius',
        graph_info     => 'This graph shows the degrees Celsius of temperature of SwtichBot sensor. The sensor accuracy is +/-0.4 deg (-20 deg - 0 deg), +/-0.2 deg (0 deg - 65 deg), +/-0.3 deg (65 deg - 80 deg).',
    },
    BatteryVoltage => {
        title_of_value => 'Remaining Battery',
        graph_args     => '--base 1000 --lower-limit 0 --upper-limit 100',
        graph_vlabel   => '%',
        graph_info     => 'This graph shows the percentage of remaining battery of SwitchBot.',
    },
};


# say 'no' for autoconf
if ($ARGV[0] and $ARGV[0] eq 'autoconf') {
    print "no (you should setup manually this plugin\n";
    exit 0;
}

my $listpath = $ENV{'listpath'} || '/etc/switchbot/devices.txt';
my $datadir  = $ENV{'datadir'}  || '/run/switchbot';

# get label and info strings from device list file
my $label_and_info = load_devices_from_list($listpath);

# load sensor values from switchbot.service data directory
my $device_values = load_device_values($datadir);


# output config and actual values

if ($ARGV[0] and $ARGV[0] eq 'config') {
    print_config($label_and_info);
    exit 0;
}

print_graphs($device_values);

# end of plugin execution
exit 0;


sub load_devices_from_list {
    my $listpath = shift;
    my $label_and_info;

    open my $fh, '<', $listpath or croak "Failed to open '$listpath' to read: $!";
    foreach my $line (<$fh>) {
        chomp $line;
        if ($line =~ /\A([0-9a-fA-F:]+) +([^ ]+) +(.+)\z/) {
            $label_and_info->{$1} = {
                label => $2,
                info  => $3,
            };
        }
        else {
            carp "Cannot parse: '$line'";
        }
    }

    return $label_and_info;
}


sub load_device_values {
    my $datadir = shift;
    my $device_values;
    
    opendir(DIR, $datadir) or croak "Failed to opendir '$datadir' $!";
    my @macs = grep {/\A[0-9a-fA-F:]+\z/} readdir(DIR);
    closedir(DIR);

    foreach my $mac (@macs) {
        open my $fh, '<', "$datadir/$mac" or croak "Failed to open '$datadir/$mac' to read: $!";
        foreach my $line (<$fh>) {
            chomp $line;
            if ($line =~ /\A(\w+) +(\-?[0-9\.]+)\z/) {
                $device_values->{$mac}{$1} = $2;
            }
            else {
                carp "Cannot parse: '$line'";
            }
        }
    }

    return $device_values;
}


sub print_config {
    my $label_and_info = shift;

    foreach my $datatype (sort keys %$CONFIG_MESSAGE) {
        my $message = $CONFIG_MESSAGE->{$datatype};
        print <<"EOF";
multigraph ${plugin_name}_${datatype}
graph_title SwtichBot $message->{'title_of_value'} per device
graph_args $message->{'graph_args'}
graph_vlabel $message->{'graph_vlabel'}
graph_category sensors
graph_info $message->{'graph_info'}
EOF
        foreach my $mac (sort keys %$label_and_info) {
            my $label = $label_and_info->{$mac}{'label'};
            my $info  = $label_and_info->{$mac}{'info'};
            my $fieldname = clean_fieldname($mac);
            print <<"EOF";
${fieldname}.label $label
${fieldname}.type GAUGE
${fieldname}.info $info ($mac)
EOF
        }
    }
}


sub print_graphs {
    my $device_values = shift;

    foreach my $datatype (sort keys %$CONFIG_MESSAGE) {
        print << "EOF";
multigraph ${plugin_name}_${datatype}
EOF
        foreach my $mac (sort keys %$device_values) {
            my $fieldname = clean_fieldname($mac);
            print << "EOF";
${fieldname}.value $device_values->{$mac}{$datatype}
EOF
        }
    }
}
